
def handleAppInfo(*gin.Context c) (

    string appname = c.Param("appname").toLower()
    object app = getAppInfo(appname)
    if app.ok == false (
        c.JSON(404, app)
        return
    )

    object stats = fs.ReadFile("./stats.json").JsonParse()
    object views = stats.views
    views[appname] = views[appname].toNum() + 1
    stats.views = views
    fs.WriteFile("./stats.json", stats.JsonFormat())

    c.JSON(200, app)
)

def handleAppIcon(*gin.Context c) (
    c.File(fs.JoinPath("./apps", c.Param("appname").toLower(), "icon.icn"))
)

def handleOwnedApps(*gin.Context c) (
    string username = c.Get("username").toArray()[1].toStr().toLower()
    if username == "" (
        c.JSON(401, {"error": "Unauthorized"})
        return
    )
    object ownerships = readOwnerships()
    array appNames = ownerships.getKeys()
    array ownedApps = []
    for i appNames.len (
        string name = appNames[i]
        if ownerships[name].contains(username) (
            ownedApps.append(name)
        )
    )
    c.JSON(200, {"ownedApps": ownedApps})
)

def handleAppFile(*gin.Context c) (
    string appname = c.Param("appname").toLower()
    string fileType = c.Param("filetype")

    string path = fs.JoinPath("./apps", appname, "script." ++ fileType.toLower())
    boolean exists = fs.Exists(path)

    app := getAppInfo(appname)
    if !exists or app.ok == false (
        c.JSON(404, {
            ok: false,
            error: "app not found",
        })
        return
    )

    info := app.info

    string username = c.MustGet("username").toStr().toLower()
    string token = c.Query("auth")

    object ownerships = readOwnerships()
    rawOwners := ownerships[appname]
    if rawOwners == null (
        rawOwners = []
    )
    array owners = rawOwners
    boolean alreadyOwns = false
    if username != "" (
        alreadyOwns = owners.contains(username.toLower())
    )

    array authors = info.authors
    if authors.len == 0 (
        c.JSON(400, { ok: false, error: "app has no authors" })
        return
    )
    for i authors.len (
        string author = authors[i]
        if author == username (
            alreadyOwns = true
        )
    )

    if info.price != null and info.price > 0 and alreadyOwns == false (
        if token == "" (
            c.JSON(401, { ok: false, error: "missing auth token" })
            return
        )
        // minus one to account for tax
        object resp = requests.Post("https://api.rotur.dev/me/transfer?auth=" ++ token, {
            body: {
                amount: info.price.toNum() - 1,
                to: info.authors[1],
                note: "App store: " ++ info.title ++ " purchase",
            }
        })
        if resp.success == false (
            c.JSON(400, { ok: false, error: "failed to purchase: " ++ resp.body.toStr().JsonParse().error })
            return
        )
    )

    if username != "" and alreadyOwns == false (
        owners.append(username.toLower())
        ownerships[appname] = owners
        writeOwnerships(ownerships)
    )

    object stats = fs.ReadFile("./stats.json").JsonParse()
    object downloads = stats.downloads
    downloads[appname] = downloads[appname].toNum() + 1
    stats.downloads = downloads
    fs.WriteFile("./stats.json", stats.JsonFormat())

    c.File(path)
)

def handleAllApps(*gin.Context c) (
    string token = c.Query("auth")
    string username = ""
    if token != "" (
        array parts = token.split(",")
        if parts.len >= 2 (
            username = parts[2].toStr().toLower()
        )
    )

    object resp = getAllApps(false)
    if resp.ok == false (
        c.JSON(500, resp)
        return
    )

    if username != "" (
        object ownerships = readOwnerships()
        object apps = resp.apps
        array names = apps.getKeys()
        for i names.len (
            string n = names[i]
            array owners = ownerships[n]
            boolean owns = owners != null and owners.contains(username)
            apps[n].owns = owns
        )
        resp.apps = apps
    )

    c.JSON(200, resp)
)

def handleAppScreenshot(*gin.Context c) (
    string appname = c.Param("appname").toLower()
    string screenshot = c.Param("screenshot")

    string path = fs.JoinPath("./apps", appname, "screenshots", screenshot)
    boolean exists = fs.Exists(path)

    if !exists (
        c.JSON(404, {
            ok: false,
            error: "app not found",
        })
        return
    )

    c.File(path)
)

def handleFeatured(*gin.Context c) (

    object featured = loadConfig("featured.json")
    if featured.featured == null (
        c.JSON(404, {
            ok: false,
            error: "featured app not found",
        })
        return
    )

    c.JSON(200, featured)
)

def handleGetAppReviews(*gin.Context c) (
    string appname = c.Param("appname").toLower()

    c.JSON(200, getReviews(appname))
)

def handleAppReview(*gin.Context c) (
    string appname = c.Param("appname").toLower()
    string username = c.MustGet("username")

    string review = c.Query("content")
    if review == "" (
        c.JSON(400, { ok: false, error: "Missing content" })
    )

    writeReview(appname, username, review)
    c.JSON(200, { ok: true, error: "Successfully added review" })
)